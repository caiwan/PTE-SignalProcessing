<?xml version="1.0" encoding="utf-8"?>
<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en-US" lang="en-US">
<head>
	<title></title>
	<link rel="stylesheet" type="text/css" href="style.css" />
	<link href="jquery/css/ui-lightness/jquery-ui-1.10.4.custom.css" rel="stylesheet">
	
	<script type="text/javascript" src="gl-matrix.js"></script>
	<script type="text/javascript" src="caiwan-wgl-framework.js"></script>
	<script src="jquery/js/jquery-1.10.2.js"></script>
	<script src="jquery/js/jquery-ui-1.10.4.custom.js"></script>
	
	<script id="shader-fs" type="x-shader/x-fragment">
#ifdef GL_ES
precision mediump float;
#endif

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

uniform mat4 fpar;

#define M_PI 3.14159265359

// Created by inigo quilez - iq/2013
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

// A list of usefull distance function to simple primitives, and an example on how to 
// do some interesting boolean operations, repetition and displacement.
//
// More info here: http://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm

float sdPlane( vec3 p )
{
	return p.y+0.2;
}

float sdSphere( vec3 p, float s )
{
    return length(p)-s;
}

float udRoundBox( vec3 p, vec3 b, float r )
{
  return length(max(abs(p)-b,0.0))-r;
}

float length2( vec2 p )
{
	return sqrt( p.x*p.x + p.y*p.y );
}

float length6( vec2 p )
{
	p = p*p*p; p = p*p;
	return pow( p.x + p.y, 1.0/6.0 );
}

float length8( vec2 p )
{
	p = p*p; p = p*p; p = p*p;
	return pow( p.x + p.y, 1.0/8.0 );
}

//----------------------------------------------------------------------

float opS( float d1, float d2 )
{
    return max(-d2,d1);
}

float opU( float d1, float d2 )
{
	return (d1<d2) ? d1 : d2;
}


vec2 opU( vec2 d1, vec2 d2 )
{
	return (d1.x<d2.x) ? d1 : d2;
}

vec3 opRep( vec3 p, vec3 c )
{
    return mod(p,c)-0.5*c;
}

vec3 opTwist( vec3 p )
{
    float  c = cos(10.0*p.y+10.0);
    float  s = sin(10.0*p.y+10.0);
    mat2   m = mat2(c,-s,s,c);
    return vec3(m*p.xz,p.y);
}

vec3 opTwist( vec3 p, float tc, float ts )
{
    float  c = cos(tc*p.y+ts);
    float  s = sin(tc*p.y+ts);
    mat2   m = mat2(c,-s,s,c);
    return vec3(m*p.xz,p.y);
}

vec3 opTwist2( vec3 p, float tc, float ts, float cc)
{
    float  c = cos(cc*tc*p.y+ts);
    float  s = sin(cc*tc*p.y+ts);
    mat2   m = mat2(c,-s,s,c);
    return vec3(m*p.xz,p.y);
}

vec3 opTwist3( vec3 p, float tc, float ts, float cc)
{
    float  c = cos(sin(tc*p.y)*cc*tc*p.y+ts);
    float  s = sin(sin(tc*p.y)*cc*tc*p.y+ts);
    mat2   m = mat2(c,-s,s,c);
    return vec3(m*p.xz,p.y);
}

vec3 opBend(vec3 p, float g, float q)
{
    float  cx = cos(q*g);
    float  sx = sin(q*g);
    mat2   m = mat2(cx,-sx,sx,cx);
    return vec3(m*p.xy,p.z);
}

//----------------------------------------------------------------------

float hash(float x){
	return fract(sin(2224.4234*x)*78546.68565);
}

float noise(float x){
	float ff = floor(x);
	float fr = fract(x);
	return mix(hash(ff), hash(ff+1.), fr*fr*(3.-2.*fr)) * 2. - 1.;
}

// noise +2 oct
float noise2(float x){
	return .5*noise(x) + .25*noise(1.+2.*x) + .125 * noise(2.+4.*x);
}

//----------------------------------------------------------------------

vec3 textureMap(in float m, in vec3 p){
	if(m == 1.0){ // padlo
		vec2 a = 1.05 * abs(1. - 2.*mod(p.xz, 1.));
		return mix(vec3(.7,.7,.7), vec3(.5,.5,.7), clamp(pow(a.y, 50.)+pow(a.x, 50.),0.,1.));
	} else if(m == 2.0){ // blob
		return vec3(.5,.5,.2);
	}
	return vec3(0);
}

vec2 map( in vec3 pos )
{
	vec4 mod = vec4(vec3(0.1,0.1,0.1) + fpar[3].xyz * vec3(0., 0., 0.01), fpar[3].w/400.);
	// fpar[3].xy szabad !
	vec3 	ppos = pos - vec3(0., mod.z - .1, 0.);
	vec3 	bpos = opBend(ppos, ppos.y, M_PI*(fpar[2].x-50.)/50.);
			bpos = opBend(bpos, ppos.z, M_PI*(fpar[2].y-50.)/50.);
			bpos = opBend(bpos, ppos.x, M_PI*(fpar[2].z-50.)/50.);
	
			// ez megfekteti a shader forditot:
			//bpos -= vec3(0.,1.,1.) * vec3 (noise2(ppos.x), noise2(50.+ppos.x), noise2(2500.+ppos.z)) * (fpar[2].z-50.)/50.;
	
	vec2 res = opU
	(
		vec2(sdPlane(pos), 	// padlo
			1.0 			// padlo - color index
		),
		
		vec2(udRoundBox(	// hasab
				opTwist(bpos , 
					(fpar[3].x-50.) / 10., (fpar[3].y-50.) /10.
				),
				mod.xyz, mod.w	// modulacio
			),	
			2.0	// color index
		)
	);
	
	return res;
}

//----------------------------------------------------------------------

vec2 castRay( in vec3 ro, in vec3 rd, in float maxd )
{
    float precis = 0.001;
    float h=precis*2.0;
    float t = 0.0;
    float m = -1.0;
    for( int i=0; i<60; i++ )
    {
        if( abs(h)<precis||t>maxd ) break;
        t += h;
	    vec2 res = map( ro+rd*t );
        h = res.x;
	    m = res.y;
    }

    if( t>maxd ) m=-1.0;
    return vec2( t, m );
}


float softshadow( in vec3 ro, in vec3 rd, in float mint, in float maxt, in float k )
{
	float res = 1.0;
    float dt = 0.02;
    float t = mint;
    for( int i=0; i<20; i++ )
    {
		if( t<maxt )
		{
        float h = map( ro + rd*t ).x;
        res = min( res, k*h/t );

		t += 0.02;
			
		}
    }
    return clamp( res, 0.0, 1.0 );

}

vec3 calcNormal( in vec3 pos )
{
	vec3 eps = vec3( 0.001, 0.0, 0.0 );
	vec3 nor = vec3(
	    map(pos+eps.xyy).x - map(pos-eps.xyy).x,
	    map(pos+eps.yxy).x - map(pos-eps.yxy).x,
	    map(pos+eps.yyx).x - map(pos-eps.yyx).x );
	return normalize(nor);
}

float calcAO( in vec3 pos, in vec3 nor )
{
	float totao = 0.0;
    float sca = 1.0;
    for( int aoi=0; aoi<5; aoi++ )
    {
        float hr = 0.01 + 0.05*float(aoi);
        vec3 aopos =  nor * hr + pos;
        float dd = map( aopos ).x;
        totao += -(dd-hr)*sca;
        sca *= 0.75;
    }
    return clamp( 1.0 - 4.0*totao, 0.0, 1.0 );
}

vec3 render( in vec3 ro, in vec3 rd )
{ 
    vec3 col = vec3(0.0);
    vec2 res = castRay(ro,rd,20.0);
    float t = res.x;
	float m = res.y;
    if( m>-0.5 )
    {
        vec3 pos = ro + t*rd;
        vec3 nor = calcNormal( pos );
		
		col = textureMap(m, pos);
		
		//col = vec3(.1, .1, .4);
		vec3 col2 = vec3(.4, .4, .02);
		
        float ao = calcAO( pos, nor );

		vec2 rot_lig = (1. + fpar[1][0]) * vec2(sin(M_PI*fpar[1][1] / 50.), cos(M_PI*fpar[1][1] / 50.)) / 5.;
		vec3 lig_src = vec3( 
			vec3(-rot_lig.x, 1.+fpar[1][2]/25., rot_lig.y) 
		);
		
		vec3 lig  = normalize(lig_src);
		
		float amb = clamp( 0.5+0.5*nor.y, 0.0, 1.0 );
        float dif = clamp( dot( nor, lig ), 0.0, 1.0 );
		//float dif = clamp( .5 + .5*dot( nor, lig ), 0.0, 1.0 );
        float bac = clamp( dot( nor, normalize(vec3(-lig.x,0.0,-lig.z))), 0.0, 1.0 )*clamp( 1.0-pos.y,0.0,1.0);

		float sh = 1.0;
		if( dif>0.02 ) { sh = softshadow( pos, lig_src, 0.02, 10.0, .25+fpar[1].w/12.5 ); dif *= sh; }

		vec3 brdf = vec3(0.0);
		
		brdf += 0.20*amb*vec3(0.20,0.00,0.00)*ao;	// ambient
		brdf += 0.20*bac*vec3(0.15,0.0,0.15)*ao;	// back light
		brdf += 1.20*dif*vec3(1.00,0.90,0.60);		// diffusal
		
		float pp = clamp( dot( reflect(rd,nor), lig ), 0.0, 1.0 );
		float spe = sh*pow(pp,16.0);	// specular
		float fre = ao*pow( clamp(1.0+dot(nor,rd),0.0,1.0), 2.0 );

		//col = col*brdf + vec3(1.0)*col*spe + 0.2*fre*(0.5+0.5*col);
		col = col*brdf + vec3(1.0)*col*spe + mix(col,col2, 0.4*fre);
	}

	col *= exp( -0.01*t*t*(fpar[0].w/100.) );
	/*col *= col;*/
	
	return vec3( clamp(col,0.0,1.0) );
}

void main( void )
{
	vec2 q = gl_FragCoord.xy/resolution.xy;
    vec2 p = -1.0+2.0*q;
	p.x *= resolution.x/resolution.y;
    vec2 mo = mouse.xy/resolution.xy;
		 
	float tm = 15.0 + time;

	vec2 rot = (1. + fpar[0][0]) * vec2(sin(M_PI*fpar[0][1] / 50.), cos(M_PI*fpar[0][1] / 50.)) / 12.5;
	// camera
	vec3 ro = vec3( rot.x, 1.+fpar[0][2] / 50., rot.y);	
	vec3 ta = vec3( 0.0, 0.0, 0.0 );
	
	// camera tx
	vec3 cw = normalize( ta-ro );
	vec3 cp = vec3( 0.0, 1.0, 0.0 );
	vec3 cu = normalize( cross(cw,cp) );
	vec3 cv = normalize( cross(cu,cw) );
	vec3 rd = normalize( p.x*cu + p.y*cv + 2.5*cw );
	
    vec3 col = render( ro, rd );

	/*col = sqrt( col );*/

    gl_FragColor=vec4( col, 1.0 );
}
</script>

<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    void main(void) {
        gl_Position = vec4(aVertexPosition, 1.0);
    }
</script>
	
	<script>

// valtozokat ellenoriz	
function checkAndTrim(_value, _min, _max){
	obj = { error : 0, value : _value, min : _min, max : _max };
	
	if(isNaN(obj.value)) {obj.error = 1; obj.value = obj.min; return obj};
	if(obj.value<obj.min){obj.error = 2; obj.value = obj.min; return obj;}
	if(obj.value>obj.max){obj.error = 3; obj.value = obj.max; return obj;}
	
	return obj;
}
	
var App = {
	renderer : 0,
	stuff : 0,

	//matrix: [],
	matrixNames : [],

	//////////////////////////////////////////////////////
	initRenderer : function(renderer){ 
		var shader = new Shader("shader-vs", "shader-fs");
		
		// beallitjuk az inputokat
		var inputs = new Input();
		
		// fullscreen quad
		var screenQuad = new Buffer(new Float32Array([
             1.0,  1.0,  0.0,
            -1.0,  1.0,  0.0,
             1.0, -1.0,  0.0,
            -1.0, -1.0,  0.0
		]), 3, 4);
		
		return {
			renderer : renderer,
			shader : shader,
			input : inputs,
			screenQuad : screenQuad,
			matrix : [],
			//app : App
		};
	},
	
	mainloop: function (time, dd){
		gl.viewport(0, 0, gl.viewportWidth, gl.viewportHeight);
        gl.clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);
		gl.clearColor(0.0, 0.0, 0.0, 1.0);
        //gl.enable(gl.DEPTH_TEST);
		
		dd.shader.bind();
		
		dd.shader.enableAttribArray("aVertexPosition");
		dd.screenQuad.bindToAttrib(dd.shader, "aVertexPosition");
		
		dd.shader.setUniform1f("time", time/1000.);
		
		//console.log(dd.app);
		
		dd.shader.setUniform4m("fpar", dd.matrix);
		
		canvas = dd.renderer.canvas;
		dd.shader.setUniform2f("resolution", canvas.width, canvas.height);
		
		gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);
		
		dd.shader.disableAttribArray("aVertexPosition");
		dd.shader.unbind();
		
	},
	
	//////////////////////////////////////////////////////
	main : function(){
		// tagek hozzaadasa
		// ez elegge ronda megoldas, hogy jquery es vanilla js keverve van jelen
		var mainDiv, mainCanvas;
		
		mainDiv = $("#renderSurface");
		mainCanvas = document.createElement('canvas');
		mainCanvas.id = 'glcanvas';
		
		mainDiv.html(mainCanvas);
		
		// uj renderer letrehozasa
		var renderer = new Renderer(mainCanvas.id);
		App.resizeCanvas(mainDiv, mainCanvas, renderer);
		
		// renderer dolgainak inicializalasa
		this.stuff = App.initRenderer(renderer);
		
			//console.log(this.stuff);
		
		// ui-k
		this.createUI();
		
		// callback beallit
		renderer.mainloopCallback = function(time){App.mainloop(time, App.stuff);};
		renderer.showFPSCallback = function(FPS){$("#ShowFPS").html("FPS: " + FPS);};
		// mainloop indit
		renderer.mainLoop();
	},
	
	resizeCanvas: function (div, canvas, renderer){
		canvas.width = div.width();
		canvas.height = div.height();
		
		renderer.resizeViewport();
	},
	
	//////////////////////////////////////////////////////////////
	
	createUI:function(){
		var i = 0;
		$( "#uniformArray > span" ).each(function() {
			var ss = "uniformArray"+i;
			$(this).attr("id",ss);
			App.stuff.matrix[i] = 50;
			App.matrixNames[ss] = i;
			i++;
			$(this).empty().slider({
				value: 50,
				range: "min",
				orientation: "vertical",
				slide: function(evt, ui){
					App.stuff.matrix[App.matrixNames[ui.handle.parentElement.id]] = ui.value;
					//console.log(App.stuff.matrix);
				}
			});
		});
	},
}

// ha betoltodott az oldal es a jquery
$(function() {
	App.main();
});
	</script>
</head>
<body>
	<!-- HEADER -->
	<header>
		<h1>Kép és hangfeldolgozás
			<em>Raymarcher teszt</em>
		</h1>
		<em>Sári Péter / SAPSAAP.PTE</em>
	</header>
	
	<!-- CONTENT -->
	<section class="guest">
		
		<div id="renderSurface"></div>
		
		<div id="UIComponents">
			<div id="uniformArray">
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
				<span></span>
			</div>
			
		</div>
		
		<h3 id="ShowFPS"></h3>
	</section>
	
	<!-- FOOTER -->
	<footer><a href = "http://ir.untergrund.net/~caiwan/">Caiwan/IR</a> / 2014 may</footer>
</body>